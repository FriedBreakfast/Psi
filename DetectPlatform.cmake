include(CheckSymbolExists)

# Detect native platform
set(PSI_HOST_CPU_VERSION 0)
if(CMAKE_SYSTEM_PROCESSOR MATCHES "^arm")
  set(PSI_HOST_CPU "arm")
  set(PSI_HOST_CPU_VERSION 4)
  
  if(CMAKE_SYSTEM_PROCESSOR MATCHES "^armv[0-9]+")
    string(REGEX MATCH "[0-9]+" PSI_HOST_CPU_VERSION ${CMAKE_SYSTEM_PROCESSOR})
  endif()
elseif(CMAKE_SIZEOF_VOID_P EQUAL 8)
  set(PSI_HOST_CPU "x86_64")
else()
  set(PSI_HOST_CPU "i386")
endif()

if(WIN32)
  set(PSI_HOST_OS "win")
else()
  set(PSI_HOST_ABI "gnu")
  
  if(PSI_HOST_CPU STREQUAL "arm")
    check_symbol_exists(__ARM_EABI__ "stdio.h" DEFINED_ARM_EABI)
    if(DEFINED_ARM_EABI)
      check_symbol_exists(__ARM_PCS_VFP "stdio.h" DEFINED_ARM_EABIHF)
      if (DEFINED_ARM_EABIHF)
        set(PSI_HOST_ABI "gnueabihf")
      else()
        set(PSI_HOST_ABI "gnueabi")
      endif()
    endif()
  endif()

  if(CMAKE_SYSTEM_NAME STREQUAL "Linux")
    set(PSI_HOST_OS "linux")
  else()
    set(PSI_HOST_OS "unknown")
  endif()
endif()
